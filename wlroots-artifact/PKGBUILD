# Maintainer: Paraworker <paraworker@gmail.com>

pkgname=wlroots-artifact
_pkgname=wlroots
pkgver=0.17.0+134+ga0b1329e
pkgrel=1
license=('MIT')
pkgdesc='Modular Wayland compositor library'
url='https://gitlab.freedesktop.org/wlroots/wlroots'
arch=('x86_64')

depends=(
    'hwdata'
    'libdisplay-info'
    'libglvnd'
    'libinput'
    'libliftoff'
    'seatd' 'libseat.so'
    'libxcb'
    'libxkbcommon' 'libxkbcommon.so'
    'opengl-driver'
    'pixman' 'libpixman-1.so'
    'systemd-libs' 'libudev.so'
    'vulkan-icd-loader' 'libvulkan.so'
    'wayland'
    'xcb-util-errors'
    'xcb-util-renderutil'
    'xcb-util-wm'
)

makedepends=(
    'glslang'
    'meson'
    'ninja'
    'systemd'
    'vulkan-headers'
    'wayland-protocols'
    'xorg-xwayland'
)

optdepends=(
    'xorg-xwayland: Xwayland support'
)

provides=(
    'libwlroots.so'
)

options=(
    'debug'
)

source=("git+https://gitlab.freedesktop.org/wlroots/wlroots.git"
        #"https://gitlab.freedesktop.org/wlroots/wlroots/-/merge_requests/4321.patch"
        )

sha256sums=('SKIP'
            #'SKIP'
            )

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd "$srcdir/$_pkgname"
  #patch -p1 < "$srcdir/4321.patch"
}

build() {
    arch-meson $_pkgname build
    meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
  install -Dm644 "$_pkgname/LICENSE" -t "$pkgdir/usr/share/licenses/$_pkgname/"
}
